plugins {
    id "com.android.application"
    id "kotlin-android"
    id "io.gitlab.arturbosch.detekt"
}

android {
    namespace "com.louis993546.seattle"
    defaultConfig {
        applicationId "com.louis993546.seattle"
        minSdk min_sdk_version
        compileSdk compile_sdk_version
        targetSdk compile_sdk_version
        def number = System.getenv("GITHUB_RUN_NUMBER") ?: 1
        versionCode number.toInteger()
        versionName "0.${number}.0"
    }
    signingConfigs {
        debug {
            storeFile rootProject.file("debug.keystore")
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion kotlin_compiler_version
    }
}

dependencies {
    def composeBom = platform("androidx.compose:compose-bom:$compose_bom_version")
    implementation composeBom
    androidTestImplementation composeBom
    implementation "androidx.activity:activity-compose:1.8.0"
    implementation "androidx.compose.ui:ui-tooling"
    implementation "com.google.android.material:material:1.10.0"

    implementation project(path: ":metro")
    implementation project(path: ":demoAppRow") // TODO move it into metro maybe?

    implementation "com.jakewharton.timber:timber:5.0.1"

    testImplementation "junit:junit:4.13.2"
}