plugins {
    id "com.android.application"
    id "kotlin-android"
    id "io.gitlab.arturbosch.detekt"
}

android {
    namespace "com.louis993546.metro.demo"
    compileSdk compile_sdk_version

    buildToolsVersion "$build_tool_version"

    defaultConfig {
        applicationId "com.louis993546.metro.demo"
        minSdk min_sdk_version
        compileSdk compile_sdk_version
        targetSdk compile_sdk_version
        def number = System.getenv("GITHUB_RUN_NUMBER") ?: 1
        versionCode number.toInteger()
        versionName "0.${number}.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        debug {
            storeFile rootProject.file("debug.keystore")
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion kotlin_compiler_version
    }
}

dependencies {
    def composeBom = platform("androidx.compose:compose-bom:$compose_bom_version")
    implementation composeBom
    androidTestImplementation composeBom

    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "com.google.android.material:material:1.8.0"
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.material:material"
    implementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.0"
    implementation "androidx.activity:activity-compose:1.6.1"
    implementation project(path: ":metro")
    implementation "com.jakewharton.timber:timber:5.0.1"
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation project(path: ':demoApps')

    //region Apps
    implementation project(path: ':demoAppDrawer')
    implementation project(path: ':demoAppSearch')
    implementation project(path: ':demoBrowser')
    implementation project(path: ':demoCalculator')
    implementation project(path: ':demoCalendar')
    implementation project(path: ':demoLauncher')
    implementation project(path: ':demoMetroSettings')
    implementation project(path: ':demoRadio')
    implementation project(path: ':demoSettings')
    implementation project(path: ':demoWordle')
    //endregion

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += "-opt-in=org.mylibrary.OptInAnnotation"
    }
}
